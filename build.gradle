buildscript {
    ext.repos = {
        mavenLocal()
        mavenCentral()
        maven {
            url velox_repo
            credentials {
                username velox_repo_user
                password velox_repo_password
            }
        }
    }

    repositories repos

    dependencies {
        classpath 'com.velox:velox-gradle-plugin:(,0.7.0]'
    }

    configurations.classpath.resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
}

apply plugin: 'com.velox.velox-gradle-plugin'

repositories repos

dependencies {
    implementation 'com.velox:velox-framework:(,1.6.0]'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

configurations {
    all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

application {
    mainClass = 'com.velox.starter.Application'
}

distributions {
    main {
        contents {
            from 'src/main/webapp', {
                into 'src/main/webapp'
            }
        }
    }
}

idea.module {
    excludeDirs += file('.vertx')
    excludeDirs += file('.local')
    downloadJavadoc = true
}
