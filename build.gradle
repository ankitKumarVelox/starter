buildscript {
    dependencies {
        classpath 'com.velox:velox-gradle-plugin:0.8.0+'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
}

apply plugin: 'com.velox.velox-gradle-plugin'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url velox_repo_url
        credentials {
            username velox_repo_user
            password velox_repo_password
        }
    }
}

dependencies {
    implementation 'com.velox:velox-framework:2.2.0+'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.11'
}

application {
    mainClass.set('com.velox.starter.Application')
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

/**
 * Customize the start script generated by the application plugin to shorten
 * the classpath by pointing to the lib folder rather than listing individual
 * jars one by one. The latter usually has an issue on Windows where the
 * command argument length is limited
 */
startScripts {
    classpath = files('$APP_HOME/lib/*')
}

/**
 * Package the src/main/webapp folder into the distribution zip as the folder
 * contains web related resource files.
 */
distributions {
    main {
        contents {
            from 'src/main/webapp', {
                into 'src/main/webapp'
            }
        }
    }
}

idea.module {
    excludeDirs += file('.vertx')
    excludeDirs += file('.local')
    downloadJavadoc = true
}
